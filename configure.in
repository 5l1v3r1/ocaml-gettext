##########################################################################
#  Ocaml-gettext : a library to translate messages                       #
#                                                                        #
#  Copyright (C) 2001 Jean-Christophe FILLIATRE                          #
#  Copyright (C) 2003, 2004, 2005 Sylvain Le Gall <sylvain@le-gall.net>  #
#                                                                        #
#  This library is free software; you can redistribute it and/or         #
#  modify it under the terms of the GNU Lesser General Public            #
#  License as published by the Free Software Foundation; either          #
#  version 2.1 of the License, or (at your option) any later version;    #
#  with the OCaml static compilation exception.                          #
#                                                                        #
#  This library is distributed in the hope that it will be useful,       #
#  but WITHOUT ANY WARRANTY; without even the implied warranty of        #
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU     #
#  Lesser General Public License for more details.                       #
#                                                                        #
#  You should have received a copy of the GNU Lesser General Public      #
#  License along with this library; if not, write to the Free Software   #
#  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307   #
#  USA                                                                   #
#                                                                        #
#  Contact: sylvain@le-gall.net                                          #
##########################################################################

AC_INIT()

AC_ARG_WITH(docdir,
	AC_HELP_STRING([--with-docdir=dir], [Location of documentation ( default is ${datadir}/doc )]),
	DOCDIR=$withval, DOCDIR=${datadir}/doc)

AC_ARG_WITH(builddir,
	AC_HELP_STRING([--with-builddir=dir], [Location of the build ( default is ${topdir}/build )]),
	BUILDDIR=$withval, BUILDDIR=`pwd`/build)

AC_ARG_WITH(defaultlocaledir,
	AC_HELP_STRING([--with-defaultlocaledir=dir], [Location of the default locale dir ( default is /usr/share/locale )]),
	DEFAULTLOCALEDIR="\"$withval\"", DEFAULTLOCALEDIR="\"/usr/share/locale\"")

AC_ARG_WITH(localedir,
	AC_HELP_STRING([--with-localedir=dir], [Additional location of the locale dir ( default is /usr/local/share/locale )]),
	LOCALEDIR=$LOCALEDIR "\"$withval\";", LOCALEDIR="\"/usr/local/share/locale\"")

AC_ARG_WITH(stub-cflags,
	AC_HELP_STRING([--with-stub-cflags=flags], [Flags to use when compiling gettext-stub]),
	STUB_CFLAGS=$withval, STUB_CFLAGS="")

AC_ARG_WITH(stub-ldflags,
        AC_HELP_STRING([--with-stub-ldflags=flags], [Flags to use when linking gettext-stub. Default: -L(OCAMLLIB) -lcamlidl]),
        STUB_LDFLAGS=$withval, STUB_LDFLAGS="")

AC_ARG_WITH(docbook-stylesheet,
        AC_HELP_STRING([--with-docbook-stylesheet=dir], [Where to find the docbook stylesheets]),
        DOCBOOK_STYLESHEET=$withval, DOCBOOK_STYLESHEET="/usr/share/xml/docbook/stylesheet/nwalsh")

AC_ARG_ENABLE(camomile,
        AC_HELP_STRING([--enable-camomile], [Build camomile extension. Default : yes]),
        BUILD_CAMOMILE=$enableval, BUILD_CAMOMILE="yes")

AC_ARG_ENABLE(stub,
        AC_HELP_STRING([--enable-stub], [Build stub extension. Default : yes]),
        BUILD_STUB=$enableval, BUILD_STUB="yes")

VERSION=0.1.1
DATE=`date`

# check if we build at least one of stub | camomile for building ocaml-gettext

if test "$BUILD_STUB" = no && test "$BUILD_CAMOMILE" = no; then
  AC_MSG_WARN(Stub and camomile extension build, cannot build ocaml-gettext executable.)
fi

# check if we found the docbook stylesheet

for stylesheets in xhtml/chunk.xsl manpages/docbook.xsl fo/docbook.xsl; do 
  if ! test -e "$DOCBOOK_STYLESHEET/$stylesheets"; then
    AC_MSG_ERROR(Cannot find $DOCBOOK_STYLESHEET/$stylesheets.)
  fi
done

# check for xsltproc presence 

AC_CHECK_PROG(XSLTPROC,xsltproc,xsltproc,no)
if test "$XSLTPROC" = no ; then
  AC_MSG_WARN(Cannot find xsltproc.)
fi

# check for xmllint

AC_CHECK_PROG(XMLLINT,xmllint,xmllint,no)
if test "$XMLLINT" = echo ; then
  AC_MSG_WARN(Cannot find xmllint.)
fi

# check for ocamldoc 

AC_CHECK_PROG(OCAMLDOC,ocamldoc,ocamldoc,no)
if test "$OCAMLDOC" = no ; then
  AC_MSG_WARN(Cannot find ocamldoc.)
fi

# check for fop

AC_CHECK_PROG(FOP,fop,fop,no)
if test "$FOP" = no ; then
  AC_MSG_WARN(Cannot find fop.)
fi

# check for Ocaml compilers

AC_CHECK_OCAMLC([],[AC_MSG_ERROR(Cannot find ocamlc.)])
AC_CHECK_OCAMLOPT([],[AC_MSG_WARN(Cannot find ocamlopt, byte compilation only)])
AC_CHECK_OCAMLLEX([],[AC_MSG_ERROR(Cannot find ocamllex.)])
AC_CHECK_OCAMLYACC([],[AC_MSG_ERROR(Cannot find ocamlyacc.)])
AC_LIB_OCAML()

if test "x$STUB_LDFLAGS" = "x"; then
  STUB_LDFLAGS="-L$OCAMLLIB -lcamlidl"
fi

# platform
AC_MSG_CHECKING(platform)
if echo "let _ = Sys.os_type" | ocaml | grep -e Win32; then
    AC_MSG_RESULT(Win32)
    OCAMLWIN32=yes
    EXE=.exe
else
    AC_MSG_RESULT(NotWin32)
    OCAMLWIN32=no
    EXE=
fi

# we look for ocamlfindlib in the path; if not present, we fail
AC_CHECK_PROG(OCAMLFIND,ocamlfind,ocamlfind,no)
if test "$OCAMLFIND" = no ; then
	AC_MSG_ERROR(Cannot find ocamlfind.)
fi

AC_CHECK_PROG(OCAMLP4,camlp4,camlp4,no)
if test "$OCAMLP4" = no ; then
        AC_MSG_ERROR(Cannot find camlp4.)
fi

AC_CHECK_PROG(CAMLIDL,camlidl,camlidl,no)
if test "$CAMLIDL" = no ; then
        AC_MSG_ERROR(Cannot find camlidl.)
fi

AC_CHECK_PROG(OCAMLMKLIB,ocamlmklib,ocamlmklib,no)
if test "$OCAMLMKLIB" = no ; then
        AC_MSG_ERROR(Cannot find ocamlmklib.)
fi

AC_CHECK_PROG(MKCAMLP4,mkcamlp4,mkcamlp4,no)
if test "$MKCAMLP4" = no ; then
	AC_MSG_ERROR(Cannot find mkcamlp4.)
fi

# substitutions to perform
AC_SUBST(BUILDDIR)
AC_SUBST(DOCDIR)
AC_SUBST(OCAMLC)
AC_SUBST(OCAMLOPT)
AC_SUBST(OCAMLDEP)
AC_SUBST(OCAMLLEX)
AC_SUBST(OCAMLYACC)
AC_SUBST(OCAMLBEST)
AC_SUBST(OCAMLVERSION)
AC_SUBST(OCAMLLIB)
AC_SUBST(OCAMLWEB)
AC_SUBST(OCAMLWIN32)
AC_SUBST(OCAMLFIND)
AC_SUBST(OCAMLP4)
AC_SUBST(MKCAMLP4)
AC_SUBST(CAMLIDL)
AC_SUBST(OCAMLMKLIB)
AC_SUBST(EXE)
AC_SUBST(DEFAULTLOCALEDIR)
AC_SUBST(LOCALEDIR)
AC_SUBST(CODESET)
AC_SUBST(DATE)
AC_SUBST(VERSION)
AC_SUBST(STUB_CFLAGS)
AC_SUBST(STUB_LDFLAGS)
AC_SUBST(DOCBOOK_STYLESHEET)
AC_SUBST(BUILD_CAMOMILE)
AC_SUBST(BUILD_STUB)
AC_SUBST(XSLTPROC)
AC_SUBST(XMLLINT)
AC_SUBST(OCAMLDOC)
AC_SUBST(FOP)

# Finally create the Makefile from Makefile.in
OUTPUT="ConfMakefile                \
  libgettext-camomile-ocaml/META    \
  libgettext-ocaml/gettextConfig.ml \
  libgettext-ocaml/META             \
  libgettext-stub-ocaml/META"

AC_OUTPUT([$OUTPUT])
